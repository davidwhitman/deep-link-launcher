buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'
//apply plugin: 'kotlin-android-extensions'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    signingConfigs {
        // Debug keystore keeps signature consistent across CI builds
        debug {
            storeFile file('debugKeystore.jks')
            storePassword 'password'
            keyAlias 'key0'
            keyPassword 'password'
        }

        def keysFile = file('keystore.properties')

        release {
            storeFile file('releaseKeystore.jks')
            keyAlias 'key0'

            if (keysFile.canRead()) {
                def props = new Properties()
                props.load(new FileInputStream(keysFile))
                storePassword props.releaseKeystorePassword
                keyPassword props.releaseKeystoreKeyPassword
            } else {
                storePassword "${System.env.releaseKeystorePwd}"
                keyPassword "${System.env.releaseKeystoreKeyPwd}"
            }
        }

        releasePlayStore {
            storeFile file('releaseKeystore.jks')
            keyAlias 'key0'

            if (keysFile.canRead()) {
                def props = new Properties()
                props.load(new FileInputStream(keysFile))
                storePassword props.releaseKeystorePassword2
                keyPassword props.releaseKeystoreKeyPassword2
            } else {
                storePassword "${System.env.releaseKeystorePwd2}"
                keyPassword "${System.env.releaseKeystoreKeyPwd2}"
            }
        }
    }

    defaultConfig {
        applicationId "com.thunderclouddev.deeplink"
        minSdkVersion 21
        targetSdkVersion 25
        resConfig "en"
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        releasePlayStore {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.releasePlayStore
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        maxProcessCount 4
        threadCount 16
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding.enabled = true
}

dependencies {
    provided 'org.jetbrains:annotations:13.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'

    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.github.hotchemi:android-rate:1.0.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.getkeepsafe.taptargetview:taptargetview:1.5.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jetbrains.anko:anko-sdk21:0.8.2'

    // Copied into project as SortedListAdapter
//    compile 'com.github.wrdlbrnft:sorted-list-adapter:0.2.0.1'

    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1'

    compile 'com.bluelinelabs:conductor:2.0.4'
    // If you want the components that go along with
    // Android's support libraries (currently just a PagerAdapter):
    //    compile 'com.bluelinelabs:conductor-support:2.0.4'

    kapt 'com.android.databinding:compiler:2.2.0'

    releasePlayStoreCompile 'com.google.firebase:firebase-database:9.0.2'
    releasePlayStoreCompile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'

    compile project(':qrcodelib')
}

kapt.generateStubs = true