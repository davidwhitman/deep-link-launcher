buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

kapt {
    generateStubs = true
}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    signingConfigs {
        // Debug keystore keeps signature consistent across CI builds
        debug {
            storeFile file('debugKeystore.jks')
            storePassword 'password'
            keyAlias 'key0'
            keyPassword 'password'
        }

        def keysFile = file('keystore.properties')

        release {
            storeFile file('releaseKeystore.jks')
            keyAlias 'key0'

            if (keysFile.canRead()) {
                def props = new Properties()
                props.load(new FileInputStream(keysFile))
                storePassword props.releaseKeystorePassword
                keyPassword props.releaseKeystoreKeyPassword
            } else {
                storePassword "${System.env.releaseKeystorePwd}"
                keyPassword "${System.env.releaseKeystoreKeyPwd}"
            }
        }

        releasePlayStore {
            storeFile file('releaseKeystore.jks')
            keyAlias 'key0'

            if (keysFile.canRead()) {
                def props = new Properties()
                props.load(new FileInputStream(keysFile))
                storePassword props.releaseKeystorePassword2
                keyPassword props.releaseKeystoreKeyPassword2
            } else {
                storePassword "${System.env.releaseKeystorePwd2}"
                keyPassword "${System.env.releaseKeystoreKeyPwd2}"
            }
        }
    }

    defaultConfig {
        applicationId "com.thunderclouddev.deeplink"
        minSdkVersion 21
        targetSdkVersion 25
        resConfig "en"

        versionCode System.getenv("CIRCLE_BUILD_NUM") as Integer ?: 1
        versionName "0.0.1"
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        releasePlayStore {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.releasePlayStore
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        maxProcessCount 4
        threadCount 16
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding.enabled = true
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    def supportLibVersion = "25.1.1"
    def requeryVersion = "1.1.2"
    provided 'org.jetbrains:annotations:13.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    kapt "com.android.databinding:compiler:$gradle_version"

    compile 'com.google.code.gson:gson:2.8.0'

    compile 'com.google.dagger:dagger:2.8'
    kapt 'com.google.dagger:dagger-compiler:2.8'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jetbrains.anko:anko-sdk21:0.8.2'

    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1'

    compile 'com.bluelinelabs:conductor:2.0.4'

    // Requery Database
    compile "io.requery:requery:$requeryVersion"
    compile "io.requery:requery-android:$requeryVersion"
    compile "io.requery:requery-kotlin:$requeryVersion"
    kapt "io.requery:requery-processor:$requeryVersion"

    releasePlayStoreCompile 'com.google.firebase:firebase-database:9.0.2'
    releasePlayStoreCompile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'

    compile project(':qrcodelib')
}